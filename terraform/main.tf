# AWS Provider Configuration
provider "aws" {
  region = var.aws_region
  shared_credentials_files = [var.aws_credentials_file]
  profile = var.aws_profile
}

# Create VPC
resource "aws_vpc" "app_vpc" {
  cidr_block           = var.vpc_cidr
  enable_dns_hostnames = true
  enable_dns_support   = true

  tags = {
    Name = "app-vpc"
  }
}

# Create Internet Gateway
resource "aws_internet_gateway" "app_igw" {
  vpc_id = aws_vpc.app_vpc.id

  tags = {
    Name = "app-igw"
  }
}

# Create Public Subnet
resource "aws_subnet" "public_subnet" {
  vpc_id                  = aws_vpc.app_vpc.id
  cidr_block              = var.public_subnet_cidr
  map_public_ip_on_launch = true
  availability_zone       = "${var.aws_region}a"

  tags = {
    Name = "public-subnet"
  }
}

# Create Route Table
resource "aws_route_table" "public_rt" {
  vpc_id = aws_vpc.app_vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.app_igw.id
  }

  tags = {
    Name = "public-route-table"
  }
}

# Associate Route Table with Subnet
resource "aws_route_table_association" "public_rt_assoc" {
  subnet_id      = aws_subnet.public_subnet.id
  route_table_id = aws_route_table.public_rt.id
}

# Create Security Group for EC2
resource "aws_security_group" "app_sg" {
  name        = "app-security-group"
  description = "Allow HTTP, HTTPS, SSH and custom TCP"
  vpc_id      = aws_vpc.app_vpc.id

  # SSH
  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
    description = "SSH"
  }

  # HTTP
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
    description = "HTTP"
  }

  # HTTPS
  ingress {
    from_port   = 443
    to_port     = 443
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
    description = "HTTPS"
  }

  # Application ports
  ingress {
    from_port   = 8080
    to_port     = 8090
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
    description = "Application ports"
  }

  # Allow all outbound traffic
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "app-security-group"
  }
}

# Create EC2 instance
resource "aws_instance" "app_server" {
  ami                    = var.ami_id
  instance_type          = var.instance_type
  key_name               = aws_key_pair.app_key_pair.key_name
  vpc_security_group_ids = [aws_security_group.app_sg.id]
  subnet_id              = aws_subnet.public_subnet.id

  root_block_device {
    volume_size = 50
    volume_type = "gp2"
  }

  tags = {
    Name = "app-server"
  }

  # Connection settings for remote provisioners
  connection {
    type        = "ssh"
    user        = "ubuntu"
    private_key = file(var.private_key_path)
    host        = self.public_ip
    timeout     = "5m"
  }

  # Create Ansible inventory
  provisioner "local-exec" {
    command = <<-EOT
      cat > ../ansible/inventory.ini << EOF
      [app_servers]
      app ansible_host=${self.public_ip} ansible_user=ubuntu ansible_ssh_private_key_file=${var.private_key_path}
      EOF
    EOT
  }

  # Run Ansible playbook
  provisioner "local-exec" {
    command = "sleep 120 && ANSIBLE_HOST_KEY_CHECKING=False ANSIBLE_TIMEOUT=300 ANSIBLE_CONNECT_TIMEOUT=60 ANSIBLE_SSH_RETRIES=5 ansible-playbook -i ../ansible/inventory.ini ../ansible/playbook.yml"
  }
}

# Create SSH Key Pair
resource "aws_key_pair" "app_key_pair" {
  key_name   = "app-key-pair"
  public_key = file(var.public_key)
}

# DNS Records
resource "aws_route53_record" "main" {
  zone_id = var.hosted_zone_id
  name    = var.domain_name
  type    = "A"
  ttl     = "300"
  records = [aws_instance.app_server.public_ip]
}

resource "aws_route53_record" "subdomains" {
  for_each = toset(["auth", "todos", "users"])

  zone_id = var.hosted_zone_id
  name    = "${each.key}.${var.domain_name}"
  type    = "A"
  ttl     = "300"
  records = [aws_instance.app_server.public_ip]
}

# Output values
output "public_ip" {
  value = aws_instance.app_server.public_ip
}

output "public_dns" {
  value = aws_instance.app_server.public_dns
}

output "domain_name" {
  value = var.domain_name
}